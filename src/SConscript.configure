Import('env', 'pyoutput')

pyversion = pyoutput('import sys; print("%i.%i" % sys.version_info[:2])')

# Helper functions -----------------------------------------------------------

def configure_boost_library(libname):
    '''Add a boost library to the configured environment allowing for any
    of the boostlibtags name extensions.

    libname  -- boost library name without any extension

    Note: CheckLib function automatically adds library to the environment.
    '''
    mttags = ['', '-mt']
    boostlibtags = mttags
    # check more tags for boost_python
    if libname == 'boost_python':
        major, minor = pyversion.split('.')
        pytags = [major + minor, major, '']
        boostlibtags = [(pt + mt) for mt in mttags for pt in pytags]
    # using global conf defined below
    for t in boostlibtags:
        libnamefull = libname + t
        if conf.CheckLib(libnamefull, language='C++'):
            return
    # library not found here
    print('This program requires %r library' % libname)
    Exit(1)

# Start configuration --------------------------------------------------------

conf = Configure(env)

# python shared library
pylibname = 'python' + pyversion
if not conf.CheckLib(pylibname, language='C++'):
    print('This program requires %r shared library' % pylibname)
    Exit(1)

# boost_python
configure_boost_library('boost_python')
configure_boost_library('boost_filesystem')
configure_boost_library('boost_system')

env = conf.Finish()
Export('env')

# vim: ft=python
